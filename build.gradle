
plugins {
    id 'edu.wpi.first.NativeUtils' version '1.6.7'
    id "edu.wpi.first.GradleJni" version "0.2.2"
	id "cpp"
	id "java"
	id "eclipse"
}

ext.ctre_library_version = "5.1.2.1"
ext.release_version = "V1_" + ctre_library_version

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release/"
    }
    maven {
        url 'https://raw.githubusercontent.com/Open-RIO/Maven-Mirror/master/m2'
    }
}

ext.allwpilibVersion = {
    return '2018.4.1'
}

ext.getWpiUtilVersion = {
	return '3.2.0'
}

sourceSets {
	main {
		java {
			srcDirs += "ctre_source/main/java"
		}
	}
}


apply from: "config.gradle"
model {

	components {
		CtreSimulation(JniNativeLibrarySpec) {
			baseName = 'CTRE_PhoenixCCI'

			enableCheckTask true
			javaCompileTasks << compileJava
			jniCrossCompileOptions << JniCrossCompileOptions('athena')
			sources {
				cpp {
					source {
						srcDirs = ["src/main/native/cpp"]
						includes = ["**/*.cpp"]
					}
					exportedHeaders {
						srcDirs = ["src/main/native/include", "ctre_source/main/native/include"]
					}
				}
			}
		}
	}
}


task packageNativeFilesInJar(type: Jar) {
	destinationDir = project.buildDir
	classifier = "native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

	project.model {
		binaries {
			withType(SharedLibraryBinarySpec) { binary ->
				if (binary.component.name == "CtreSimulation")
				{
                    dependsOn binary.buildTask
					from(binary.sharedLibraryFile) {

						into NativeUtils.getPlatformPath(binary)
					}
				}
			}
		}
	}
}


dependencies {

	testCompile 'edu.wpi.first.wpiutil:wpiutil-java:' + getWpiUtilVersion()
    testCompile 'edu.wpi.first.wpilibj:wpilibj-java:' + allwpilibVersion()
    compile 'edu.wpi.first.wpilibj:wpilibj-jni:' + allwpilibVersion() + ":all"

	testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
	testCompile 'org.junit.jupiter:junit-jupiter-params:5.2.0'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
	testRuntime 'org.junit.platform:junit-platform-launcher:1.2.0'

	compile 'org.apache.logging.log4j:log4j-api:2.11.0'
	compile 'org.apache.logging.log4j:log4j-core:2.11.0'

	testRuntime packageNativeFilesInJar.outputs.files
	testCompile "openrio.mirror.third.ctre:CTRE-phoenix-java:" + ctre_library_version
}

test {
	useJUnitPlatform {
	}
}

jar {
	exclude('com/ctre/**')
}

javadoc {
	failOnError = false
}


eclipse.classpath.file.whenMerged { classpath ->
	classpath.entries.each {
		if(it.path.contains("CtreSimulator")) {
			it.setNativeLibraryLocation("$rootDir/build/libs/ctreSimulation/shared")
		}
	}
}

tasks.withType(Test) {
    systemProperty "java.library.path", "$rootDir/build/libs/ctreSimulation/shared"
}

apply from: "publish.gradle"
